<?xml version="1.0" encoding="utf-8"?>
<mx:VBox xmlns:mx="http://www.adobe.com/2006/mxml" creationComplete="init()">

<!-- creationComplete="init()" -->

    <mx:Style>
<!--
  **        
  ** @font-face {
  **     src: url("location") | local("name");
  **     fontFamily: alias;
  **     [fontStyle: normal | italic | oblique;]
  **     [fontWeight: normal | bold | heavy;]
  **     [advancedAntiAliasing: true | false;]
  ** }
  **        
  -->

      @font-face
      {
		font-family: DefaultFont;
        src: url("../../fonts/TIMES.TTF");
      }
      @font-face
      {
		font-family: default;
        src: url("../../fonts/myriadweb.ttf");
      }
      @font-face
      {
		font-family: defaultBold;
        src: url("../../fonts/myriadwebbol.ttf");
        font-weight: bold;
      }
      @font-face
      {
		font-family: myriad;
        src: url("../../fonts/myriadweb.ttf");
      }
      @font-face
      {
		font-family: myriadBold;
        src: url("../../fonts/myriadwebbol.ttf");
        font-weight: bold; 
     }

      @font-face
      {
		font-family: arial;
        src: url("../../fonts/Arial.ttf");
      }
      @font-face
      {
		font-family: arialBold;
        src: url("../../fonts/Arial_Bold.ttf");
        font-weight: bold; 
      }

      @font-face
      {
		font-family: freesans;
        src: url("../../fonts/FreeSans.ttf");
      }
      @font-face
      {
		font-family: freesansBold;
        src: url("../../fonts/FreeSansBold.ttf");
        font-weight: bold; 
      }

      .defaultBoldStyle {
        font-family: DefaultFont;
        font-weight: bold;
      } 
    </mx:Style>

    <mx:Script><![CDATA[
    	import mx.containers.TitleWindow;
    	import mx.controls.Label;
    	import mx.events.CloseEvent;

	import multigraph.Graph;
	import multigraph.renderer.RendererList;
    import flash.external.ExternalInterface;

    import mx.core.Container;
	import mx.containers.Panel;
	import mx.managers.PopUpManager;

	import mx.controls.Alert;

	private var _swfname:String;
    private var aboutWindow:TitleWindow;

    [Bindable]
    public var test:String = "";

    [Bindable]
    public var rendererlist:String = "";

    [Bindable]
    public var swfname:String = "";

    [Bindable]
    public var hostname:String = "";

    [Bindable]
    public var pathname:String = "";

    [Bindable]
    public var port:String = "";

    [Bindable]
    public var proxy:String = "";
    
    [Bindable]
    public var muglfile:String = "";
    
    private var myMenu:ContextMenu;

    public static function create(muglfile:String):Multigraph {
 	   var m:Multigraph = new Multigraph();
	   m.muglfile = muglfile;
       m.init();
       return m;
    }

	public function init():void {
		if (test=="true") {
		} else if (rendererlist=="true") {
			new RendererList();
		} else {
			this._swfname = swfname;
  			var loader:URLLoader = new URLLoader();
    		loader.dataFormat = "text";
    		loader.addEventListener( Event.COMPLETE, 
				function(event:Event):void {
                    createGraphs(event.target.data, 
                                 swfname, hostname, pathname, port, proxy);
				}
			);
    		loader.load( new URLRequest( proxiedUrl( proxy, muglfile ) ) ); 
		}
		myMenu = new ContextMenu();
		myMenu.hideBuiltInItems();
		myMenu.builtInItems.print = true;
		var item:ContextMenuItem = new ContextMenuItem("About Multigraph");
		item.addEventListener(ContextMenuEvent.MENU_ITEM_SELECT, showAboutWindow);
		myMenu.customItems.push(item);
		this.contextMenu = myMenu;
//		stage.scaleMode = StageScaleMode.NO_SCALE;
//    	stage.align = StageAlign.TOP_LEFT;
	}


	private function showAboutWindow(evt:Event):void {
	   aboutWindow = new AboutMultigraph();
       PopUpManager.addPopUp(aboutWindow, this.parent, true);
       PopUpManager.centerPopUp(aboutWindow);
       this.cursorManager.removeAllCursors();
    }
            
    private function aboutWindow_close(evt:CloseEvent):void {
        PopUpManager.removePopUp(aboutWindow);
    }

    private function createGraphs(xmlText:String, 
                                  swfname:String, hostname:String, pathname:String, port:String, proxy:String):void {
      var graph:Graph;
      try {

        // Get the xml object for the the mugl file
		var xml:XML = new XML( xmlText );

        // If there is no <graph> element in the mugl file, create a single Graph.
        if (xml.graph == null || xml.graph.length() == 0) {

          // Check to see if there are "width"/"height" attributes
          // on a <window> tag in the mugl file, and if so, use those
          // as the width and height of the graph.
          var graphWidth:int=-1; var graphHeight:int=-1;
          if (xml.window != null && xml.window[0] != null) {
            if (xml.window[0].@width != null) {
              graphWidth = int(xml.window[0].@width);
            }
            if (xml.window[0].@height != null) {
              graphHeight = int(xml.window[0].@height);
            }
          }

          // If the width/height weren't set in the mugl file, use the
          // width and height given as mxml properties, unless those properties
          // weren't given, in which case we use the width/height of the stage
          if (graphWidth<=0) {
             if (this.width > 0) {
                graphWidth = this.width;
             } else {
                graphWidth = stage.width;
             }
          }
          if (graphHeight<=0) {
             if (this.height > 0) {
                graphHeight = this.height;
             } else {
                graphHeight = stage.height;
             }
          }

          graph = new Graph(xml, swfname, hostname, pathname, port, proxy, graphWidth, graphHeight);
          multigraphContainer.addChild(graph);

        // There is at least one <graph> element in the mugl file, so loop over each one,
        // creating a graph with the given size and position for each one.
        } else {
          for (var i:int = 0; i<xml.graph.length(); ++i) {
            graph = new Graph(xml.graph[i],
                              swfname, hostname, pathname, port, proxy,
                              xml.graph[i]['@width'], xml.graph[i]['@height']
                              );
            graph.x = xml.graph[i]['@x'];
            graph.y = xml.graph[i]['@y'];
            multigraphContainer.addChild(graph);
          }
        }
      } catch (e:TypeError) {
        ///app.displayMessage("problem with XML file!!!");
        trace("Could not parse text into XML");
        trace(e.message);
      }    
    }

    public static function createPopUp(mugl:String, width:int, height:int, title:String, parent:DisplayObject, proxy:String=null, modal:Boolean=false) {
        var titleWindow:ClosableTitleWindow = new ClosableTitleWindow();
        titleWindow.title = title;

        var mGraph:Multigraph = new Multigraph();
        mGraph.muglfile = mugl;
		mGraph.width    = width;
		mGraph.height   = height;
		mGraph.proxy    = proxy;

        titleWindow.addChild(mGraph);
        PopUpManager.addPopUp(titleWindow, parent, modal);
   }
   
   public static function proxiedUrl(proxy:String, url:String):String {
   	if (proxy != null && proxy!="" && url.substr(0, 7)=="http://") {
   		url = url.substring(7, url.length-1);
   		if (proxy.charAt(proxy.length-1)=="/") {
   			return proxy + url;
   		} else { 
   			return proxy + "/" + url;
   		}
   	}
   	return url;
   }

  ]]></mx:Script>
	
	<mx:Canvas id="multigraphContainer">
	</mx:Canvas>
</mx:VBox>
